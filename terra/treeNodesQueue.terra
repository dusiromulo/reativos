#include "/home/terra/TerraNG/terra/TerraNet.defs"

#define MSG_TYPE_SETUP 0
#define MSG_TYPE_SEND_TEMP 1

pktype myMsg from radioMsg with
	var ubyte[4] d8;
	var ushort[4] d16;
	var ulong[2]  d32;
end

var ushort thisParent = 0;
var ushort thisID = getNodeId();

if thisID == 11 then
	thisParent = 1;
else
	var myMsg receivedSetupMsg;
	receivedSetupMsg = await RECEIVE(MSG_TYPE_SETUP);
	thisParent = receivedSetupMsg.source;
end

var myMsg sendSetupMsg;
sendSetupMsg.type = MSG_TYPE_SETUP;
sendSetupMsg.target = BROADCAST;
sendSetupMsg.source = thisID;

emit SEND(sendSetupMsg);
await SEND_DONE;

par do
	loop do
		var myMsg receivedTempMsg;
		receivedTempMsg = await RECEIVE(MSG_TYPE_SEND_TEMP);
		receivedTempMsg.target = thisParent;
		
		qPut(receivedTempMsg);
	end
with
	var ushort tempCount = 0;
	var ushort timeWaiting = random() % 5 + 4;
	loop do
		await (timeWaiting)s;
		var myMsg sendTempMsg;

		emit REQ_TEMP();
		sendTempMsg.type = MSG_TYPE_SEND_TEMP;
		sendTempMsg.d16[0] = tempCount;
		sendTempMsg.d16[1] = await TEMP;

		sendTempMsg.source = thisID;
		sendTempMsg.target = thisParent;

		qPut(sendTempMsg);

		inc tempCount;
	end
with
	loop do
		await Q_READY;
		loop do
			if qSize() > 0 then
				var myMsg queueMsg;
				qGet(queueMsg);

				emit SEND(queueMsg);
				await SEND_DONE;
				await 200ms;
			else
				break;
			end
		end
	end
end